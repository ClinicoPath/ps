	real function mhsize[dllexport](alpha,power,t,theta,lambda,rho,pi,corrected)
	USE MSIMSL
	implicit none
!
!  Calculate sample size (number of subjects) for Mantel-Haenszel test.
!  Sample size is derived by interating to find the zero of the mhpower
!  routine.
!
!   input:
!       alpha=type I error
!       power=power for the uncorrected or corrected M-H chi-square
!       t=number of 2X2 tables
!       theta=common odds ratio accross tables that is to be detected
!       lambda(i)=proportion of subjects in table i:i=1,..,t
!       rho(i)=proportion of cases in ith table
!       pi(i,2)=proportion of successes among controls in table i
!       corrected=uncorrected or corrected result (0 or 1)
!

	real power, t, lambda(20), rho(20), pi(20,2), theta, alpha
	integer corrected

	real cpower, ct, clambda(20), crho(20), cpi(20,2), ctheta, calpha
	integer ccorrected
	common /mhsizecomm/ cpower, ct, clambda, crho, cpi, ctheta, calpha, ccorrected

	integer i
	integer    itmax, nroot
	real       eps, errabs, errrel, eta
	parameter  (nroot=1)
	integer    info(nroot)
	real       powerfcn, n(nroot), nguess(nroot)
	external powerfcn
	data nguess/100/
!
!  Code:
!
!  Load input for mhpower function into common.
!
	cpower=power
	ct=t
	do i=1,t
		clambda(i)=lambda(i)
		crho(i)=rho(i)
		cpi(i,2)=pi(i,2)
	end do
	ctheta=theta
	calpha=alpha
	ccorrected=corrected
!
!  Set zero finding parameters.
!
	eps    = 1.0e-5
	errabs = 1.0e-5
	errrel = 1.0e-5
	eta    = 1.0e-2
	itmax  = 100
!
!  Iterate for the sample size.
!
	call zreal (powerfcn, errabs, errrel, eps, eta, nroot, itmax, nguess, n, info)

	mhsize=n(nroot)

	return
	end
	
	real function powerfcn(n)
!
!  This is function to find the zero for when determining the
!  M-H sample size.
!
!  i.e., powerfcn=power-mhpower(...,n,...)
!
	implicit none
	real mhpower
	real n
	real cpower, ct, clambda(20), crho(20), cpi(20,2), ctheta, calpha
	integer ccorrected
	common /mhsizecomm/ cpower, ct, clambda, crho, cpi, ctheta, calpha, ccorrected
!
!  Code.
!
    write(*,*)" trying n=",n
!
!  A sample size less than zero causes mhpower to fail.
!  Protect against that here.
!
	if (n.lt.0) n=0
	powerfcn=cpower-mhpower(calpha,n,ct,ctheta,clambda,crho,cpi,ccorrected)
	return
	end