	real function mhpower[dllexport](alpha,bign,t,theta,lambda,rho,pi,corrected)
	USE MSIMSL
	implicit none
!
!  Calculate power for Mantel-Haenszel test.
!
!
!   input
!       alpha=type I error
!       bign=total number of subjects
!       t=number of 2X2 tables
!       theta=common odds ratio accross tables that is to be detected
!       lambda(i)=proportion of subjects in table i:i=1,..,t
!       rho(i)=proportion of cases in ith table
!       pi(i,2)=proportion of successes among controls in table i
!       corrected=uncorrected or corrected result (0 or 1)
!
	real bign, t, lambda(20), rho(20), pi(20,2), theta, alpha
	integer corrected

	real pibar(20), delta(20), w1(20), w2(20), w3(20), phi(20)
	real z1ma2, sum1, sum2, zu, sigma2cb, zc
	integer i
!
!  Code
!
	sum1=0
	sum2=0
	sigma2cb=0
	do i=1,t
	    pi(i,1)=theta*pi(i,2)/(theta*pi(i,2)+1.-pi(i,2))
		delta(i)=pi(i,1)-pi(i,2)
		pibar(i)=rho(i)*pi(i,1)+(1.-rho(i))*pi(i,2)
		w1(i)=lambda(i)*rho(i)*(1.-rho(i))
		w2(i)=(1.-rho(i))*pi(i,1)*(1.-pi(i,1))
		w3(i)=rho(i)*pi(i,2)*(1.-pi(i,2))
		phi(i)=pibar(i)*(1.-pibar(i))+delta(i)**2*rho(i)*(1.-rho(i))/(bign*lambda(i)-1.)
		sum1=sum1+w1(i)*delta(i)
		sum2=sum2+w1(i)*phi(i)
		sigma2cb=sigma2cb+w1(i)*(w2(i)+w3(i))
	end do
!
!  ANORIN evaluates the inverse of the standard normal (Gaussian) 
!  distribution function.
!
	z1ma2=ANORIN(1.-alpha/2.)

!
!  If corrected=0 then we want the uncorrected power.
!  If corrected=1 then the corrected result is desired.
!  ANORDF evaluates the standard normal (Gaussian) distribution function.
!
	if (corrected.eq.0) then
		zu=(sqrt(bign)*sum1-z1ma2*sqrt(sum2))/sqrt(sigma2cb)
		mhpower=ANORDF(zu)
	else if (corrected.eq.1) then
		zc=(sqrt(bign)*sum1-z1ma2*sqrt(sum2)-.05)/sqrt(sigma2cb)
		mhpower=ANORDF(zc)
	else
	    mhpower=-999
	end if

	return
	end


